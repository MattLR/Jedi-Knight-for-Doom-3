/***********************************************************************

enemy_basic.script

***********************************************************************/

#define IMP_RUNDISTANCE		192
#define IMP_WALKTURN		65
#define IMP_ATTACK_RATE		0.3
#define IMP_NOFOVTIME		1

object enemy_basic : monster_base {
	float		nextDodge;
	float		nextAttack;
	float		nextNoFOVAttack;
	entity		combat_node;

	string		range_attack_anim;
	
	// States
	void		state_Begin();
	void		state_Idle();

	// attacks
	float		check_attacks();
	void		do_attack( float attack_flags );
	void		combat_range();
	void		combat_melee();

	void		init();
	
	// torso anim states
	void		Torso_Idle();
	void		Torso_Pain();
	void		Torso_MeleeAttack();
	void		Torso_RangeAttack();
	
	// legs anim states
	void		Legs_Idle();
	void		Legs_Walk();
	void		Legs_Run();
};

/***********************************************************************

	Torso animation control

***********************************************************************/

void enemy_basic::Torso_Idle() {
	idleAnim( ANIMCHANNEL_TORSO, "stand" );
	
	eachFrame {
		if ( AI_PAIN ) { animState( ANIMCHANNEL_TORSO, "Torso_Pain", 3 ); }
	}
}

void enemy_basic::Torso_Pain() {
	string animname;
	float nextpain;
	float currenttime;
	
	animname = getPainAnim();
	playAnim( ANIMCHANNEL_TORSO, animname );

	nextpain = sys.getTime() + 0.25;
	
	while( !animDone( ANIMCHANNEL_TORSO, 6 ) ) {
		if ( AI_PAIN ) {
			currenttime = sys.getTime();
			if ( currenttime > nextpain ) {
				animState( ANIMCHANNEL_TORSO, "Torso_Pain", 3 );
			}
		}
		waitFrame();
	}

	finishAction( "pain" );
	animState( ANIMCHANNEL_TORSO, "Torso_Idle", 6 );
}

void enemy_basic::Torso_MeleeAttack() {
	playAnim( ANIMCHANNEL_TORSO, "melee_attack" );

	while( !animDone( ANIMCHANNEL_TORSO, 4 ) ) {
		waitFrame();
	}
	
	finishAction( "melee_attack" );
	animState( ANIMCHANNEL_TORSO, "Torso_Idle", 4 );
}

void enemy_basic::Torso_RangeAttack() {
	disablePain();

	playAnim( ANIMCHANNEL_TORSO, range_attack_anim );
	while( !animDone( ANIMCHANNEL_TORSO, 0 ) ) {
		lookAtEnemy( 1 );	
		waitFrame();
	}

	allowMovement( true );
	finishAction( "range_attack" );
	animState( ANIMCHANNEL_TORSO, "Torso_Idle", 4 );
}

/***********************************************************************

	Legs animation control

***********************************************************************/

void enemy_basic::Legs_Idle() {
	idleAnim( ANIMCHANNEL_LEGS, "stand" );

	eachFrame {
		if ( run && AI_FORWARD )	{ animState( ANIMCHANNEL_LEGS, "Legs_Run", 4 ); }
		if ( AI_FORWARD )			{ animState( ANIMCHANNEL_LEGS, "Legs_Walk", 4 ); }
	}
}

void enemy_basic::Legs_Walk() {
	playCycle( ANIMCHANNEL_LEGS, "walk" );
	
	eachFrame {
		if ( run && AI_FORWARD )	{ animState( ANIMCHANNEL_LEGS, "Legs_Run", 4 ); }
		if ( !AI_FORWARD )			{ animState( ANIMCHANNEL_LEGS, "Legs_Idle", 4 ); }
	}
}

void enemy_basic::Legs_Run() {
	playCycle( ANIMCHANNEL_LEGS, "run" );
	
	eachFrame {
		if ( !run && AI_FORWARD )	{ animState( ANIMCHANNEL_LEGS, "Legs_Walk", 4 ); }
		if ( !AI_FORWARD )			{ animState( ANIMCHANNEL_LEGS, "Legs_Idle", 4 ); }
	}
}

/***********************************************************************

	AI

***********************************************************************/

/*
=====================
enemy_basic::init
=====================
*/
void enemy_basic::init() {
	run_distance	= IMP_RUNDISTANCE;
	walk_turn		= IMP_WALKTURN;
	
	setState( "state_Begin" );
}

/***********************************************************************

	States

***********************************************************************/

/*
=====================
enemy_basic::state_Begin
=====================
*/
void enemy_basic::state_Begin() {
	animState( ANIMCHANNEL_TORSO, "Torso_Idle", 0 );
	animState( ANIMCHANNEL_LEGS, "Legs_Idle", 0 );	
	monster_begin();
	setMoveType( MOVETYPE_ANIM );
	setState( "state_Idle" );
}

/*
=====================
enemy_basic::state_Idle
=====================
*/
void enemy_basic::state_Idle() {
	wait_for_enemy();

	nextAttack		= 0;
	nextNoFOVAttack = 0;
	
	setState( "state_Combat" );
}

/***********************************************************************

	attacks

***********************************************************************/

/*
=====================
enemy_basic::do_attack
=====================
*/
void enemy_basic::do_attack( float attack_flags ) {
	nextNoFOVAttack = sys.getTime() + IMP_NOFOVTIME;
	if ( attack_flags & ATTACK_COMBAT_NODE ) {
		combat_ainode( combat_node );
	} else if ( attack_flags & ATTACK_MELEE ) {
		combat_melee();
	} else if ( attack_flags & ATTACK_MISSILE ) {
		combat_range();
	}
}

/*
=====================
enemy_basic::check_attacks
=====================
*/
float enemy_basic::check_attacks() {
	float range;
	float currentTime;
	float canMelee;
	float attack_flags;
	vector vel;
	float t;
	string anim;
	vector jumpTarget;
	
	attack_flags = 0;
	
	canMelee = testMeleeAttack();
	currentTime = sys.getTime();

	if ( canMelee ) {
		attack_flags |= ATTACK_MELEE;
	}

	if (  ( sys.getTime() > nextAttack ) && ( AI_ENEMY_VISIBLE  || AI_ENEMY_IN_FOV )) {
		range = enemyRange();
		
		if ( !canReachEnemy()) {

			anim = chooseAnim( ANIMCHANNEL_LEGS, "range_attack" );
			if ( testAnimMoveTowardEnemy( anim ) ) {
				if ( canHitEnemyFromAnim( anim ) ) {
					range_attack_anim = anim;
					attack_flags |= ATTACK_MISSILE;
				}
			}
		}
	}
	return attack_flags;
}

/*
=====================
enemy_basic::combat_range
=====================
*/
void enemy_basic::combat_range() {
	faceEnemy();
	animState( ANIMCHANNEL_TORSO, "Torso_RangeAttack", 4 );
	waitAction( "range_attack" );

	// don't attack for a bit
	nextAttack = DelayTime( IMP_ATTACK_RATE );
	nextNoFOVAttack = sys.getTime() + IMP_NOFOVTIME;
}


/*
=====================
enemy_basic::combat_melee
=====================
*/
void enemy_basic::combat_melee() {
	lookAtEnemy( 100 );
	faceEnemy();
	animState( ANIMCHANNEL_TORSO, "Torso_MeleeAttack", 2 );
	waitAction( "melee_attack" );
	lookAtEnemy( 1 );
	stopMove();
}

/***********************************************************************

weapon_demp2.script

***********************************************************************/

#define DEMP2_FIRERATE			0.2
#define DEMP2_LOWAMMO			25
#define DEMP2_NUMPROJECTILES	1

// blend times
#define DEMP2_IDLE_TO_LOWER		4
#define DEMP2_IDLE_TO_FIRE		1
#define DEMP2_RAISE_TO_IDLE		4
#define DEMP2_FIRE_TO_IDLE		4


object weapon_demp2 : weapon_base {
	float		next_attack;
	float		spread;
	
	void		init();
	
	void		Lower();
	void		Raise();
	void		Idle();
	void		Fire();
	void		ExitCinematic();
};

void weapon_demp2::init() {
	next_attack = 0;
	spread		= getFloatKey( "spread" );
	weaponState( "Raise", 0 );
}

void weapon_demp2::Raise() {
	weaponRising();
	playAnim( ANIMCHANNEL_ALL, "raise" );
	waitUntil( animDone( ANIMCHANNEL_ALL, DEMP2_RAISE_TO_IDLE ) );
	weaponState( "Idle", DEMP2_RAISE_TO_IDLE );
}

void weapon_demp2::Lower() {
	weaponLowering();
	playAnim( ANIMCHANNEL_ALL, "putaway" );
	waitUntil( animDone( ANIMCHANNEL_ALL, 0 ) );
	weaponHolstered();
	waitUntil( WEAPON_RAISEWEAPON );
	weaponState( "Raise", 0 );
}

void weapon_demp2::Idle() {
	float currentTime;

	if ( !ammoAvailable( ) ) {
		weaponOutOfAmmo();
	} else {
		weaponReady();
	}
	playCycle( ANIMCHANNEL_ALL, "idle" );
	while( 1 ) {
		if ( WEAPON_LOWERWEAPON ) {
			weaponState( "Lower", DEMP2_IDLE_TO_LOWER );
		}
		currentTime = sys.getTime();
		if ( ( currentTime >= next_attack ) && WEAPON_ATTACK ) {
				weaponState( "Fire", DEMP2_IDLE_TO_FIRE );
			}
			waitFrame();
		}
		
}

void weapon_demp2::Fire() {
	float ammoClip;
	float currentTime;
	
	next_attack = sys.getTime() + DEMP2_FIRERATE;
	
	if ( ammoAvailable( ) < DEMP2_LOWAMMO ) {
		startSound( "snd_lowammo", SND_CHANNEL_ITEM, true );
	}

	launchProjectiles( DEMP2_NUMPROJECTILES, spread, 0, 1.0, 1.0 );
	playAnim( ANIMCHANNEL_ALL, "fire" );
	while( !animDone( ANIMCHANNEL_ALL, DEMP2_FIRE_TO_IDLE ) ) {
		currentTime = sys.getTime();
		if ( ( currentTime >= next_attack ) && WEAPON_ATTACK && ( ammoAvailable( ) > 0 ) ) {
			weaponState( "Fire", 0 );
		}
		waitFrame();
	}

	weaponState( "Idle", DEMP2_FIRE_TO_IDLE );
}

void weapon_demp2::ExitCinematic() {
	next_attack = 0;
	weaponState( "Idle", 0 );
}

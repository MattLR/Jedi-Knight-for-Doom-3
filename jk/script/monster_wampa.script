/***********************************************************************

ai_monster_wampa.script

monster_wampa

***********************************************************************/




object monster_wampa : monster_base {
	entity		combat_node;
	
	// States
	void		state_Begin();
	void		state_Idle();

	// attacks
	float		check_attacks();
	void		do_attack( float attack_flags );

	void		combat_melee();


	void		init();
	
	// torso anim states
	void		Torso_Idle();
	void		Torso_Pain();
	void		Torso_MeleeAttack();
	
	// legs anim states
	void		Legs_Idle();
	void		Legs_Walk();
	void		Legs_Run();
};

/***********************************************************************

	Torso animation control

***********************************************************************/

void monster_wampa::Torso_Idle() {
	idleAnim( ANIMCHANNEL_TORSO, "stand" );
	setState( "state_Idle" );
	
eachFrame {
		if ( AI_PAIN ) { 
			animState( ANIMCHANNEL_TORSO, "Torso_Pain", 0 );
		 }
	}
}

void monster_wampa::Torso_Pain() {
	string animname;
	float nextpain;
	float currenttime;
	
	animname = getPainAnim();
	playAnim( ANIMCHANNEL_TORSO, animname );

	nextpain = sys.getTime() + 2.00;
	
	while( !animDone( ANIMCHANNEL_TORSO, 2 ) ) {
		if ( AI_PAIN ) {
			currenttime = sys.getTime();
			if ( currenttime > nextpain ) {
				animState( ANIMCHANNEL_TORSO, "Torso_Pain", 0 );
			}
		}
		waitFrame();
	}

	finishAction( "pain" );
	
	animState( ANIMCHANNEL_TORSO, "Torso_Idle", 2 );
}

void monster_wampa::Torso_MeleeAttack() {
	playAnim( ANIMCHANNEL_TORSO, "melee_attack" );

	while( !animDone( ANIMCHANNEL_TORSO, 4 ) ) {
		waitFrame();
	}
	
	finishAction( "melee_attack" );
	animState( ANIMCHANNEL_TORSO, "Torso_Idle", 4 );
}

/***********************************************************************

	Legs animation control

***********************************************************************/

void monster_wampa::Legs_Idle() {
	idleAnim( ANIMCHANNEL_LEGS, "idle" );

	eachFrame {
		if ( run && AI_FORWARD )	{ animState( ANIMCHANNEL_LEGS, "Legs_Run", 4 ); }
		if ( AI_FORWARD )			{ animState( ANIMCHANNEL_LEGS, "Legs_Walk", 4 ); }
		if ( AI_PAIN )				{ animState( ANIMCHANNEL_LEGS, "Legs_Pain", 4 ); }
	}
}

void monster_wampa::Legs_Walk() {
	playCycle( ANIMCHANNEL_LEGS, "walk" );
	
	eachFrame {
		if ( run && AI_FORWARD )	{ animState( ANIMCHANNEL_LEGS, "Legs_Run", 4 ); }
		if ( !AI_FORWARD )			{ animState( ANIMCHANNEL_LEGS, "Legs_Idle", 4 ); }
	}
}

void monster_wampa::Legs_Pain() {
	playCycle( ANIMCHANNEL_LEGS, "pain" );
	while( !animDone( ANIMCHANNEL_TORSO, 2 ) ) {
		waitFrame();
	}
	animState( ANIMCHANNEL_LEGS, "Legs_Idle", 4 );

}

void monster_wampa::Legs_Run() {
	playCycle( ANIMCHANNEL_LEGS, "run" );
	
	eachFrame {

		if ( !run && AI_FORWARD )	{ animState( ANIMCHANNEL_LEGS, "Legs_Walk", 4 ); }
		if ( !AI_FORWARD )			{ animState( ANIMCHANNEL_LEGS, "Legs_Idle", 4 ); }
		if ( AI_PAIN )				{ animState( ANIMCHANNEL_LEGS, "Legs_Pain", 4 ); }
	}
	animState( ANIMCHANNEL_LEGS, "Legs_Idle", 4 );
}

/***********************************************************************

	AI

***********************************************************************/

/*
=====================
monster_wampa::init
=====================
*/
void monster_wampa::init() {
	setState( "state_Begin" );
}

/***********************************************************************

	States

***********************************************************************/

/*
=====================
monster_wampa::state_Begin
=====================
*/
void monster_wampa::state_Begin() {
	animState( ANIMCHANNEL_TORSO, "Torso_Idle", 0 );
	animState( ANIMCHANNEL_LEGS, "Legs_Idle", 0 );

	monster_begin();

	setMoveType( MOVETYPE_SLIDE );	
	setState( "state_Idle" );
}

/*
=====================
monster_wampa::state_Idle
=====================
*/
void monster_wampa::state_Idle() {
	wait_for_enemy();

	setState( "state_Combat" );
}

/***********************************************************************

	attacks

***********************************************************************/

/*
=====================
monster_wampa::do_attack
=====================
*/
void monster_wampa::do_attack( float attack_flags ) {
	 if ( attack_flags & ATTACK_COMBAT_NODE ) {
		combat_ainode( combat_node );
	} else if ( attack_flags & ATTACK_MELEE ) {
		combat_melee();
	}
}

/*
=====================
monster_wampa::check_attacks
=====================
*/
float monster_wampa::check_attacks() {
	float currentTime;
	float canMelee;
	float attack_flags;
	
	attack_flags = 0;
	
	canMelee = testMeleeAttack();
	currentTime = sys.getTime();

	combat_node = getCombatNode();
	if ( combat_node ) {
			attack_flags |= ATTACK_COMBAT_NODE;
		}
	
	if ( canMelee ) {
		attack_flags |= ATTACK_MELEE;
	}
	
	return attack_flags;
}
/*
=====================
monster_wampa::combat_melee
=====================
*/
void monster_wampa::combat_melee() {
	stopMove();
	animState( ANIMCHANNEL_TORSO, "Torso_MeleeAttack", 5 );
	waitAction( "melee_attack" );
}
